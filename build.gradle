buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://dimdev.org/maven' }
        maven { url 'http://repo.spongepowered.org/maven' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.dimdev:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.1.0'
    }
}

apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'java'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.matthewprenger.cursegradle'

group 'vazkii.neat'
version '0.1.3'
archivesBaseName = 'NeatForRift'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://dimdev.org/maven' }
}

dependencies {
    implementation 'org.dimdev:rift:1.0.3-45:dev'
}

minecraft {
    version = '1.13'
    mappings = 'snapshot_5'
    runDir = 'run'
    tweakClass = 'org.dimdev.riftloader.launch.RiftLoaderClientTweaker'
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, 'mixins.neat.refmap.json'
}

curseforge {
    apiKey = System.getenv('CURSEFORGE_KEY') ?: '0'
    project {
        id = '301313'
        releaseType = 'beta'
        addGameVersion project.minecraft.version

        mainArtifact(jar) {
            relations {
                requiredDependency 'rift'
            }
        }
    }
}

// FIXME: cannot access TextFormatting
// Temporary Fix start
configurations {
    ecj
}
dependencies {
    ecj 'org.eclipse.jdt.core.compiler:ecj:4.6.1'
}

compileJava {
    options.fork = true
    options.forkOptions.with {
        executable = 'java'
        jvmArgs = ['-classpath', project.configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main', '-nowarn']
    }
}
// Temporary Fix end
